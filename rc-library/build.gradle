apply plugin: 'com.android.library'
apply plugin: 'maven'

android {
    compileSdkVersion appCompileSdkVersion
    buildToolsVersion appBuildToolsVersion

    defaultConfig {
        minSdkVersion appMinSdkVersion
        targetSdkVersion appTargetSdkVersion
        versionCode appVersionCode
        versionName appVersionName

        renderscriptTargetApi 18
        renderscriptSupportModeEnabled true
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    lintOptions {
        abortOnError false
    }

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src']
            res.srcDirs = ['res']
            assets.srcDirs = ['assets']
            jniLibs.srcDirs = ['libs']
        }
    }

    packagingOptions {
        exclude 'META-INF/maven/com.nineoldandroids/library/pom.properties'
        exclude 'META-INF/maven/com.nineoldandroids/library/pom.xml'
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile "com.android.support:appcompat-v7:$supportlibraryVersion"
    compile "com.android.support:design:$supportlibraryVersion"
    compile "com.android.support:recyclerview-v7:$supportlibraryVersion"
    compile "com.android.support:cardview-v7:$supportlibraryVersion"
    compile 'com.google.android.gms:play-services-maps:8.4.0'
    compile 'com.google.android.gms:play-services-location:8.4.0'
    compile 'com.nineoldandroids:library:2.4.0'
    compile files('libs/gson-2.5.jar')
    compile files('libs/picasso-2.5.2.jar')
    provided files('provided-libs/org.apache.http.legacy.jar')
}

//options for github
repositories {
    maven {
        url "https://raw.github.com/synergian/wagon-git/releases"
    }
}

configurations {
    deployLibrary
}

dependencies {
    deployLibrary "ar.com.synergian:wagon-git:0.3.0"
}

uploadArchives {

    repositories.mavenDeployer {
        configuration = configurations.deployLibrary
        repository(url: 'git:releases://git@github.com:reversecoder/rc-librarydemo-androidstudio.git')
        snapshotRepository(url: 'git:snapshots://git@github.com:reversecoder/rc-librarydemo-androidstudio.git')

        uniqueVersion = true

        pom.project {
            groupId = 'com.reversecoder.library'
            version = '1.0.0-SNAPSHOT'
            artifactId = 'rc-library'
            packaging 'aar'
        }
    }
}